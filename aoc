#!/usr/bin/env fish

if test -f .env
    source ./.env
else
    echo "Error: .env file not found."
    exit 1
end

if test (count $argv) -eq 0
    set CURRENT_YEAR (env TZ="America/New_York" date +%Y)
    set CURRENT_DAY (env TZ="America/New_York" date +%-d)
    set CURRENT_MONTH (env TZ="America/New_York" date +%m)
    if test $CURRENT_MONTH -ne 12; or test $CURRENT_DAY -lt 1; or test $CURRENT_DAY -gt 25
        echo "Advent of Code has not started or the current day is not valid."
        exit 1
    end
else
    set CURRENT_YEAR $argv[1]
    set CURRENT_DAY $argv[2]
end

set FOLDER "$CURRENT_YEAR/$CURRENT_DAY"
mkdir -p $FOLDER
cd $FOLDER; or begin
    echo "Error: Failed to change directory."
    exit 1
end

set URL "https://adventofcode.com/$CURRENT_YEAR/day/$CURRENT_DAY/input"
if curl -b "session=$SESSION_COOKIE" $URL -o "input"
    echo "Input downloaded successfully."
else
    echo "Failed to download input. Please check your session cookie and try again."
    exit 1
end

if not test -f "s.js"
    cat ../../template.js >s.js
end